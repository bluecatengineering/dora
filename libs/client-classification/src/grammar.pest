// integer = @{ ASCII_DIGIT+ }
integer    =  @{ (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ | ASCII_DIGIT) }

string = @{ "'" ~ ( "''" | (!"'" ~ ANY) )* ~ "'" }

ip = @{ ASCII_DIGIT{1,3} ~ "." ~ ASCII_DIGIT{1,3} ~ "." ~ ASCII_DIGIT{1,3} ~ "." ~ ASCII_DIGIT{1,3} }

hex = @{ "0x" ~ ASCII_HEX_DIGIT* }

boolean = @{ "true" | "false" }

operation = _{ equal | neq | and | or }
	equal = { "==" }
	neq = { "!=" }
	or = { "or" }
	and = { "and" }


option = { "option[" ~ integer ~ "]" }
relay = { "relay4[" ~ integer ~ "]" }

pkt = _{ pkt_mac }
    pkt_mac = @{ "pkt4.mac" }

substring = { "substring(" ~ expr ~ "," ~ integer ~ "," ~ integer ~ ")" }

expr = { prefix* ~ primary ~ postfix* ~ (operation ~ prefix* ~ primary ~ postfix* )* }

prefix = _{ not }
    not = { "not" } 

postfix  =  _{ to_hex | exists | sub_opt }
    to_hex    =   { ".hex" } 
    exists    =   { ".exists" } 
    sub_opt    =   { "." ~ option } 

primary = _{ hex | ip | integer | string | boolean | option | relay | pkt | substring | "(" ~ expr ~ ")" }
predicate = _{ SOI ~ expr ~ EOI }

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
